buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/release' }
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
    id 'org.hidetake.ssh' version '0.4.3'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'io.freefair.lombok' version '5.3.3.3'
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
    maven { url 'https://repo.spring.io/milestone' }
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

configurations {
    asciidoctorExtensions
}

dependencies {
    implementation project(':frontend')

    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-devtools"
    implementation "org.springframework.hateoas:spring-hateoas"

    implementation 'org.atmosphere:atmosphere-annotations:2.7.2'
    implementation 'org.atmosphere:wasync:2.1.7'
    implementation "javax.inject:javax.inject:1"

    runtimeOnly 'org.hsqldb:hsqldb:2.6.1'
    runtimeOnly 'org.atteo:evo-inflector:1.3'

    testImplementation 'com.jayway.jsonpath:json-path'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    testImplementation "org.springframework.restdocs:spring-restdocs-core"
    testImplementation "org.springframework.restdocs:spring-restdocs-asciidoctor"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
}

task deleteDockerDir(type: Delete) {
    delete "${buildDir}/docker"
}

task unzipBoot(type: Copy) {
    def zipFile = file("${buildDir}/libs/com.toedter.chatty-" + project.version + '.jar')
    def outputDir = file("${buildDir}/docker/app")

    from zipTree(zipFile)
    into outputDir

    def copyDetails = []
    eachFile { copyDetails << it }
    doLast {
        copyDetails.each { FileCopyDetails details ->
            def target = new File(outputDir, details.path)
            if (target.exists()) {
                target.setLastModified(details.lastModified)
            }
        }
    }
}
unzipBoot.dependsOn deleteDockerDir

task moveBootExplodedLib() {
    doLast {
        ant.move(file: "${buildDir}/docker/app/BOOT-INF/lib", toFile: "${buildDir}/docker/lib")
    }
}
moveBootExplodedLib.dependsOn unzipBoot

task createDockerfile() {
    doLast {
        def dockerfile = new File("$buildDir/Dockerfile")
        dockerfile.write 'FROM adoptopenjdk/openjdk11:alpine\n'
        dockerfile << 'COPY docker/lib/ /app/BOOT-INF/lib/\n'
        dockerfile << 'COPY docker/app/ /app/\n'
        dockerfile << 'CMD java $CHATTY_JAVA_ARGS -Xmx400m -Xms400m -cp /app/ org.springframework.boot.loader.JarLauncher\n'
        dockerfile << 'EXPOSE 8080\n'
    }
}
createDockerfile.dependsOn moveBootExplodedLib

task createBuildInfoFile() {
    doLast {
        def buildInfoFile = new File("$buildDir/resources/main/build-info.properties")
        Properties props = new Properties()
        props.setProperty('version', project.version.toString())
        props.setProperty('timestamp', project.buildTimestamp)
        props.store(buildInfoFile.newWriter(), null)
    }
}

test {
    outputs.dir snippetsDir

    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.enabled = true
    }
}

asciidoctor {
    configurations "asciidoctorExtensions"
    inputs.dir snippetsDir
    dependsOn test
}

task copyWebApp(type: Copy) {
    from('../frontend/dist/chatty') {
        include '**/*'
    }

    includeEmptyDirs = false

    into "$buildDir/resources/main/static"
}
copyWebApp.dependsOn project(':frontend').build

task copyDoc(type: Copy) {
    from "${asciidoctor.outputDir}"
    into "$buildDir/resources/main/static/docs"
}

task copyHalExplorer(type: Copy) {
    from "$projectDir/../hal-explorer/dist/hal-explorer"
    into "$buildDir/resources/main/static/hal-explorer"
}

bootJar {
    baseName = 'com.toedter.chatty'
    dependsOn createBuildInfoFile
    dependsOn copyWebApp
    dependsOn asciidoctor
    dependsOn copyDoc
    dependsOn copyHalExplorer
}




